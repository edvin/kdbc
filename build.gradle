group 'no.tornado'
version '1.0-SNAPSHOT'
description 'Simple, lightweight and elegant DSL for SQL Queries in Kotlin'

buildscript {
    ext.kotlin_version = '1.0.3'
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: "kotlin"
apply plugin: "maven"
apply plugin: "signing"

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.+', 'com.h2database:h2:1.4.192'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'KDBC'
                packaging 'jar'
                description 'Simple, lightweight and elegant DSL for SQL Queries in Kotlin'
                url 'https://github.com/edvin/kdbc'

                scm {
                    connection 'scm:svn:https://github.com/edvin/kdbc'
                    developerConnection 'scm:svn:https://github.com/edvin/kdbc'
                    url 'https://github.com/edvin/kdbc'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'edvin'
                        name 'Edvin Syse'
                        email 'es@syse.no'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}